apply plugin: 'signing'

configurations { gwtCompile }

dependencies {
    gwtCompile 'com.google.gwt:gwt-dev:2.5.1'
    compile 'com.google.gwt:gwt-user:2.5.1'
    compile 'com.google.gwt:gwt-elemental:2.5.1'
    compile 'com.google.guava:guava:15.0'
    testCompile 'junit:junit:4.11'
}

sourceSets {
    main.compileClasspath += configurations.gwtCompile
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-URL': 'https://github.com/FinamTrade/angulargwt',
                'Build-Date': new java.text.SimpleDateFormat('HH:mm:ss yyyy/MM/dd').format(new Date()),
        )
    }

    dependsOn classes
    from project.sourceSets.main.allSource
    includes = ['**/*']
}


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

def sonatypeProperties = new Properties()
sonatypeProperties.load(new FileInputStream("${System.properties['user.home']}/.sonatype.properties"))
for (String key : sonatypeProperties.stringPropertyNames()) {
    project.ext.set(key, sonatypeProperties.getProperty(key))
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'angulargwt'
                packaging 'jar'
                description 'AngularJS integration for GWT'
                url 'https://github.com/FinamTrade/angulargwt'
                inceptionYear '2013'

                scm {
                    url 'https://github.com/FinamTrade/angulargwt'
                    connection 'scm:git:https://github.com/FinamTrade/angulargwt.git'
                    developerConnection 'scm:git:git@github.com:FinamTrade/angulargwt.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                issueManagement {
                    url 'https://github.com/FinamTrade/angulargwt/issues'
                    system 'GitHub Issues'
                }

                developers {
                    developer {
                        id 'korzhevskiy'
                        name 'Andrei Korzhevskii'
                        email 'a.korzhevskiy@gmail.com'
                        url 'https://github.com/nordligulv'
                    }

                    developer {
                        id 'ylevin'
                        name 'Yury Levin'
                        email 'levinum@gmail.com'
                        url 'https://github.com/ylevin'
                    }
                }
            }
        }
    }
}
